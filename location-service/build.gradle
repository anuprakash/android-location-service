apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "1.4.0"

def siteUrl = 'https://github.com/shiftconnects/android-location-service'      // Homepage URL of the library
def gitUrl = 'https://github.com/shiftconnects/android-location-service.git'   // Git repository URL
def projectDesc = 'A background service that can be used to gather location data for your Android app.'
group = "com.shiftconnects.android.location"                                   // Maven Group ID for the artifact

Properties properties = new Properties()
properties.load(project.rootProject.file('bintray.properties').newDataInputStream())

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 21
        versionCode 1
        versionName version
    }
    buildTypes {}
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    def gpgPhrase = properties.getProperty("bintray.gpg.passphrase")
    
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "com.shiftconnects.android.location"
        desc = projectDesc
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        
        version {
            vcsTag = version
            gpg {
                sign = true
                passphrase = gpgPhrase
            }
            mavenCentralSync {
                sync = true
                user = properties.getProperty("oss.userToken")
                password = properties.getProperty("oss.userTokenValue")
                close = '1'
            }
        }
    }
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                description projectDesc
                name "Android Location Services"
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'inktomi'
                        name 'Matthew Runo'
                        email 'matt.runo@shiftconnects.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

dependencies {
    compile 'com.google.android.gms:play-services-location:6.5.87'
    compile 'com.google.android.gms:play-services-base:6.5.87'
    compile 'com.google.maps.android:android-maps-utils:0.3.4'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
